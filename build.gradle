allprojects {

    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repository.primefaces.org/" }
    }
}

subprojects{

    group = 'sample.gradle.multi'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_14
        targetCompatibility = JavaVersion.VERSION_14
    }

    configurations.all {

        // 依存するサブプロジェクトがない場合、ライブラリを参照するように切り替える
        resolutionStrategy.dependencySubstitution.all { DependencySubstitution dependency ->

            if (dependency.requested instanceof ProjectComponentSelector) {
                def targetProject = findProject(":${dependency.requested.path}")
                if (targetProject == null) {
                    dependency.useTarget "${group}:" + dependency.requested.path + ":+"
                }
            }
        }
    }

    dependencies {

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.0'
    }

}

wrapper {
    gradleVersion = '6.3'
}
