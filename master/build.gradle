import java.time.format.DateTimeFormatter;

apply plugin: 'at.bxm.svntools'

allprojects {

    apply plugin: 'java'

    group = 'sample.gradle.multi'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
}

subprojects {

    // UTのみバージョン指定をするので、SNAPSHOTを付ける
    // CT以降は付けないので、付けないままにする
    version = version == 'unspecified' ? version : version + '-SNAPSHOT'

    repositories {
        maven { url "https://repository.primefaces.org/" }
    }

    apply from: '../master/scripts/java.gradle'
    apply from: '../master/scripts/test.gradle'
    apply from: '../master/scripts/distribution.gradle'
    apply from: '../master/scripts/db-migration.gradle'

    ext {
         serverDir = file('src/server')
         serverReleaseDir = file("${serverDir}/release")
    }

    sourceSets {
        server.resources {
            srcDirs = [ file("${serverReleaseDir}/conf") ]
        }
    }
}

wrapper {
    gradleVersion = '6.3'
}

def projectIfPresent(String projectPath) {

    if(findProject(projectPath)) {
        return project(projectPath)
    }

    def projectName = projectPath.substring(1)
    return "${group}:${projectName}:latest.integration"
}

task svnTag(type: at.bxm.gradleplugins.svntools.tasks.SvnTag) {

    def now = LocalDateTime.now();
    def formatter = DateTimeFormatter.ofPattern('yyyyMMddHHmmss')
    tagName = "${formatter.format(now)}_${deployProjectName}"

    commitMessage = "${project.name}-${formatter.format(now)}"
}
