import java.time.format.DateTimeFormatter;

apply plugin: 'distribution'
apply plugin: 'at.bxm.svntools'

allprojects {

    apply plugin: 'java'

    group = 'sample.gradle.multi'

    ext {
        tmpDistsDir = buildDirectory.dir('tmpDistributions').get()
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
}

subprojects {

    apply from: '../master/scripts/java.gradle'
    apply from: '../master/scripts/test.gradle'
    apply from: '../master/scripts/distribution.gradle'
    apply from: '../master/scripts/db-migration.gradle'

    // UTのみバージョン指定をするので、SNAPSHOTを付ける
    // CT以降は付けないので、そのままにする
    version = version == 'unspecified' ? version : version + '-SNAPSHOT'

    repositories {
        maven { url "https://repository.primefaces.org/" }
    }

    ext {
        serverDir = file('src/server')
        serverReleaseDir = file("${serverDir}/release")
    }

    sourceSets {
        server.resources {
            srcDirs = [
                file("${serverReleaseDir}/conf")
            ]
        }
    }
}

wrapper {
    gradleVersion = '6.3'
}

task svnTag(type: at.bxm.gradleplugins.svntools.tasks.SvnTag) {

    def now = LocalDateTime.now();
    def formatter = DateTimeFormatter.ofPattern('yyyyMMddHHmmss')
    tagName = "${formatter.format(now)}_${project.name}"

    commitMessage = "${project.name}のタグを作成"
}

task preDistAllWeb { doLast {

    subprojects.each { subproject ->

        //Webアプリケーションのリソースは対象外
        if(subproject.plugins.hasPlugin('war')) {

            ant.copy(toDir: rootProject.tmpDistsDir,  preservelastmodified: 'true') {
                fileset(dir: subproject.tmpDistsDir)
            }
        }
    }
}}

task preDistAll { doLast {

    subprojects.each { subproject ->

        if(subproject.plugins.hasPlugin('war')) { return } // クロージャ内だとreturnがcontinueと同一

        ant.copy(toDir: rootProject.tmpDistsDir,  preservelastmodified: 'true') {
            fileset(dir: subproject.tmpDistsDir)
        }
    }

}}

distributions {

    main {
        distributionBaseName = deployRootDir

        contents {
            from tmpDistsDir
        }
    }
}

def projectIfPresent(String projectPath) {

    if(findProject(projectPath)) {
        return project(projectPath)
    }

    def projectName = projectPath.substring(1)
    return "${group}:${projectName}:latest.integration"
}
