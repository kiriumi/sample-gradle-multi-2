apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'com.bmuschko.cargo'

dependencies {

    def cargoVersion = '1.7.10'
    cargo "org.codehaus.cargo:cargo-core-uberjar:${cargoVersion}",
          "org.codehaus.cargo:cargo-ant:${cargoVersion}"

}

eclipse.wtp {
    component { contextPath  = webContextPath }
}

cargo {

    containerId = webContainerId
    port = Integer.parseInt(webPort)

    deployable {
        context = webContextPath
    }

    local {
        // 開始タスク：clean cargoStartLocal
        // 終了タスク：cargoStopLocal
        homeDir = file(webLocalApServerHome)
        startStopTimeout = 60000
    }

    remote {
        if(project.hasProperty('webHostName') && project.hasProperty('webHostUser') && project.hasProperty('webHostPassword')) {

            // タスク：cargoRedeployRemote
            hostname = webHostName
            username = webHostUser
            password = webHostPassword
        }
    }

}

// Warを作成せず、Warのデプロイのみするためのタスク
task deployWeb(dependsOn: cargoRedeployRemote)

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(deployWeb)) {
        tasks.findByName('war').enabled(false)
    }
}
