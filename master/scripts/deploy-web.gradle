apply plugin: 'com.bmuschko.cargo'

dependencies {

    def cargoVersion = '1.7.10'
    cargo "org.codehaus.cargo:cargo-core-uberjar:${cargoVersion}",
          "org.codehaus.cargo:cargo-ant:${cargoVersion}"
}

cargo {

    local {
        // 開始タスク：clean cargoStartLocal
        // 終了タスク：cargoStopLocal
        homeDir = file(webLocalApServerHome)
        startStopTimeout = 60000
    }

    remote {
        // タスク：cargoRedeployRemote
        hostname = webHostName
        username = webHostUser
        password = webHostPassword
    }

    containerId = webContainerId
    port = Integer.parseInt(webPort)

    deployable {
        context = webContextPath
    }
}

// 過去にビルドして作成したWarをデプロイするため、Warを作成させずにデプロイのみ行うタスク
task deployWeb(dependsOn: cargoRedeployRemote)

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(deployWeb)) {
        tasks.findByName('war').enabled(false)
    }
}
